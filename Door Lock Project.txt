                               
Arduino Door Lock Code

--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

#include <Keypad.h>		//To include keypad library
#include <Servo.h> 		//To include servo library
#include <LiquidCrystal.h> 	//To include LCD library


//Declaring the pin number for LCD
LiquidCrystal lcd(A5, A4, A3, A2, A1, A0);

	
//Declaring the number of rows & columns of keypad
int const rows = 4;	
int const columns = 4;	


//Defining the keys of our keypad
char keys[rows][columns] =
{

	{'1', '2', '3', 'A'},
	{'4', '5', '6', 'B'},
	{'7', '8', '9', 'C'},
	{'*', '0', '#', 'D'}
};	



//Declaring the pin number for rows & columns of keypad
byte colPins[columns] = {5, 4, 3, 2};	
byte rowPins[rows] = {9, 8, 7, 6};	


//Built-in function for keypad
//It actually initializes the internal keymap to be equal to userKeymap.
Keypad keypad = Keypad( makeKeymap(keys), rowPins, colPins, rows, columns );

 
//Setting up the password
String password= "A123";	
String pressed= "";		//Checking the entered password


//A servo motor is actually a device that has an output shaft which can be positioned at certain specific angles by sending coded signals.
//Defining the servo motor
Servo servo1;		


void setup() 
{
	Serial.begin(9600);	//This tells the Arduino to get ready to exchange messages with the Serial Monitor at a data rate of 9600 bits per second.
	lcd.begin(16,2);	//Specifying the LCD size in columns & rows
	servo1.attach(10) ; 	//Defines pin number through which servo motor is connected
	initializeLcd();	//User defined function to initialize the LCD
}



//Initializing the LCD
void initializeLcd()
{
	lcd.clear();		//clear LCD
	lcd.print("PASSWORD");	//print PASSWORD on LCD
	lcd.setCursor(1,1);	//setting cursor to row1, column1
	servo1.write(90);	//Door closed initially
}



void loop()
{
	
	char key = keypad.getKey(); 	//It reads the key pressed & assigns it to the character key

	if (int(key) != 0) 	//Checks whether any input is pressed or not
	{
		if (key == '#')	 //Here, # acts as reset key
		{
			reset(); //User defined function to reset the LCD
			initializeLcd();
		}

		else if (key == '*') 	//Here, '*' acts as Enter
		{
			checkPassword() ; 	//User defined fuction to check the entered password
		}

		else if (key =='D') 	//check if key is equal to D
		{
			lockDoor() ; //User defined function to lock the door
		}
		
		else
		{
			pressed.concat(key);	 //string concatenation
			lcd.print("*"); 	 //prints * on LCD display
			Serial.print(key);	 //print the pressed key in the serial monitor
		}
	}

}



//Resetting the LCD
void reset()
{
	pressed="";	//To clear pressed password
	lcd.clear();
	lcd.setCursor(0,0);
	lcd.print("Resetting...");
	delay(500);	//displays resetting for half a second (500 millisec)
}




//Checking the entered password
void checkPassword()
{
	if(pressed == password)	// check if pressed key matches the password
	{
		lcd.clear();
		lcd.setCursor(0,0);	//setting cursor to row0, column0
		lcd.print("Access Granted!");
		servo1.write(180);	//door opened
	}

	else
	{
		lcd.clear();
		lcd.setCursor(0,0);	
		lcd.print("Access Denied!");
		servo1.write(90);	//door closed
	}
} 



//Locking the door
void lockDoor()
{
	lcd.clear();
	lcd.setCursor(0,0);	
	lcd.print("Door Locked");
	servo1.write(90);	//door locked
}


